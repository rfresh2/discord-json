dependencies {
    compileOnly project(':api')
    compileOnly "$immutables_group:value:$immutables_version"
    annotationProcessor "$immutables_group:value:$immutables_version"
    api "$immutables_group:encode:$immutables_version"

    if (JavaVersion.current().isJava8()) {
        compileOnly "com.google.code.findbugs:annotations:3.0.1u2"
        testCompileOnly "com.google.code.findbugs:annotations:3.0.1u2"
    }
}

archivesBaseName = "discord-json-$project.name"

publishing {
    repositories {
        maven {
            name = "vc"
            url = "https://maven.2b2t.vc/releases"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId archivesBaseName
            version = project.version.toString()
            var envVer = System.getenv("PUBLISH_VERSION")
            if (envVer != null) {
                version = envVer
            }

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = "$project.name"
                description = 'Discord entity domain as immutable Jackson objects'
                url = 'https://github.com/Discord4J/discord-json'
                organization {
                    name = 'Discord4J'
                    url = 'https://discord4j.com'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/Discord4J/discord-json/issues'
                }
                licenses {
                    license {
                        name = 'LGPL-3.0'
                        url = 'https://github.com/Discord4J/discord-json/LICENSE.txt'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = 'https://github.com/Discord4J/discord-json'
                    connection = 'scm:git:git://github.com/Discord4J/discord-json.git'
                    developerConnection = 'scm:git:ssh://git@github.com:Discord4J/discord-json.git'
                }
                developers {
                    developer {
                        name = 'The Discord4J Team'
                    }
                }
            }
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': archivesBaseName,
                'Implementation-Version': archiveVersion,
                'Automatic-Module-Name': 'discord4j.discordjson.encoding'
    }
}
